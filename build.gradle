plugins {
    id 'java'
    id "io.freefair.lombok" version "5.3.0"
}

apply plugin: 'maven-publish'
apply plugin: 'signing'

group 'au.com.skytix'
version '1.0.0'

repositories {
    mavenCentral()
}

task sourceJar(type: Jar) {
    classifier "sources"
    from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier "javadoc"
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives sourceJar
    archives javadocJar
}

dependencies {
    implementation 'com.microsoft.graph:microsoft-graph:3.2.0'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.3.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.3.1'
}

test {
    useJUnitPlatform()
}

signing {
    sign configurations.archives
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            customizePom(pom)
            groupId 'au.com.skytix'
            artifactId 'onedrive-pdf-converter'
            version version

            from components.java

            // create the sign pom artifact
            pom.withXml {
                def pomFile = file("${project.buildDir}/generated-pom.xml")
                writeTo(pomFile)
                def pomAscFile = signing.sign(pomFile).signatureFiles[0]
                artifact(pomAscFile) {
                    classifier = null
                    extension = 'pom.asc'
                }
            }

            artifact(sourceJar) {
                classifier = 'sources'
            }
            artifact(javadocJar) {
                classifier = 'javadoc'
            }

            // create the signed artifacts
            project.tasks.signArchives.signatureFiles.each {
                artifact(it) {
                    def matcher = it.file =~ /-(sources|javadoc)\.jar\.asc$/
                    if (matcher.find()) {
                        classifier = matcher.group(1)
                    } else {
                        classifier = null
                    }
                    extension = 'jar.asc'
                }
            }
        }
    }
    repositories {
        maven {
            url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                username sonatypeUsername
                password sonatypePassword
            }
        }
    }
}

def customizePom(pom) {
    pom.withXml {
        def root = asNode()

        // eliminate test-scoped dependencies (no need in maven central POMs)
        root.dependencies.removeAll { dep ->
            dep.scope == "test"
        }

        // add all items necessary for maven central publication
        root.children().last() + {
            resolveStrategy = Closure.DELEGATE_FIRST

            description 'OneDrive/Sharepoint PDF Converter'
            name 'onedrive-pdf-converter'
            url 'https://github.com/skytix-dev/onedrive-pdf-converter'
            organization {
                name 'com.skytix'
                url 'https://github.com/skytix-dev'
            }
            issueManagement {
                system 'GitHub'
                url 'https://github.com/skytix-dev/onedrive-pdf-converter/issues'
            }
            licenses {
                license {
                    name 'Apache License 2.0'
                    url 'https://github.com/skytix-dev/onedrive-pdf-converter/blob/master/LICENCE'
                    distribution 'repo'
                }
            }
            scm {
                url 'https://github.com/skytix-dev/onedrive-pdf-converter'
                connection 'scm:git:git://github.com/skytix-dev/onedrive-pdf-converter.git'
                developerConnection 'scm:git:ssh://git@github.com:skytix-dev/onedrive-pdf-converter.git'
            }
            developers {
                developer {
                    name 'Marc Dergacz'
                }
            }
        }
    }
}

model {
    tasks.generatePomFileForMavenJavaPublication {
        destination = file("$buildDir/generated-pom.xml")
    }
    tasks.publishMavenJavaPublicationToMavenLocal {
        dependsOn project.tasks.signArchives
    }
    tasks.publishMavenJavaPublicationToMavenRepository {
        dependsOn project.tasks.signArchives
    }
}